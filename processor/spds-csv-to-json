# -*- cperl -*-

use strict;
use warnings;
use utf8;
use Encode ();
use JSON ();

sub {
    my $content = $_[0];

    my $csv = [split / *\n */, Encode::decode(big5 => $content)];

    my @t = $csv->[0] =~ m!資料時間：民國([0-9]+)年([0-9]+)月([0-9]+)日 ([0-9]+)點([0-9]+)分([0-9]+)秒!;
    $t[0] += 1911;

    my $p = qr/\s*,\s*/;

    my (undef, @machine) = split($p => $csv->[1]);
    my (undef, @status)  = split($p => $csv->[2]);
    my (undef, @ratio)   = split($p => $csv->[3]);
    my (undef, @power)   = split($p => $csv->[4]);

    my $status_code_to_text = {};
    for my $line (@{$csv}[7..$#$csv]) {
        my ($n,$text) = split /:/, $line;
        if (defined($n) && defined($text)) {
            $status_code_to_text->{$n} = $text;
        } else {
            last;
        }
    }

    my $localtime = sprintf("%04d-%02d-%02dT%02d:%02d:%02d+08", @t);
    my $spds = [
        map { +{
            localtime => $localtime,
            machine => $machine[$_],
            status  => 0+ $status[$_],
            ratio   => 0+ $ratio[$_],
            power   => 0+ $power[$_],
            status_text => $status_code_to_text->{ 0+ $status[$_] } // "通信維護"
        } } 0..$#machine
    ];

    return JSON->new->utf8->pretty->canonical->encode($spds);
}
